  T 130 K
   GK
[0]A 3 F
[1]T 72 @
[2]A 108 F  [LENGTH]
[3]S 107 F  [LENGTH --]
[4]T 108 F  [ПЕРЕЗАПИСЬ LENGTH]

[5]T 0 F    [ACC = 0]
[6]A 109 F  [АДРЕС ПЕРВОГО ЭЛЕМЕНТА]
[7]U 4 F    [ЗАПИСЫВАЕМ В 4 ЯЧЕЙКУ ДЛЯ ПЕРВОЙ ПЕРЕМЕННОЙ]
[8]T 5 F    [ДЛЯ ВТОРОЙ ПЕРЕМЕННОЙ]

[9]A 106 F  [0]
[10]T 6 F   [I = 0]

[11]A 106 F [0]
[12]S 108 F [-LENGTH]
[13]E 72 @  [>=0 ЗАКОНЧИЛИ ПРОХОД МАССИВА - ВЫХОД]

[14]T 0 F   [ACC = 0]
[15]A 4 F   [АДРЕС ПЕРВОГО ЭЛЕМЕНТА]
[16]L 0 D   [СДВИГ НА РАЗРЯД ВЛЕВО]
[17]A 105 F [ЗАГРУЖАЕМ КОМАНДУ A 0 S]
[18]T 19 @  [ЗАПИСЫВАЕМ ЕЁ В ЯЧЕЙКУ 19]
[19]A 0 F   [СЧИТЫВАЕМ ПЕРВЫЙ ЭЛЕМЕНТ МАССИВА]
[20]T 1 F   [ЗАПИСЫВАЕМ В ПЕРВУЮ ЯЧЕЙКУ]

[21]T 0 F   [ACC = 0]
[22]A 6 F   [I]
[23]A 107 F [I ++]
[24]T 6 F   [ПЕРЕЗАПИСЬ I]

[25]A 5 F   [СЧИТЫВАЕМ АДРЕС ПЕРВОГО СРАВНИВАЕМОГО ЧИСЛАла]
[26]A 107 F [+1]
[27]U 5 F   [ПЕРЕЗАПИСЫВАЕМ АДРЕС ВТОРОГО ЧИСЛА И ОСТАВЛЯЕМ В ACC]
[28]L 0 D   [СДВИГ НА РАЗРЯД ВЛЕВО]
[29]A 105 F [ЗАГРУЖАЕМ КОМАНДУ A 0 S]
[30]T 31 @  [ЗАПИСЫВАЕМ ЕЁ В ЯЧЕЙКУ 31]
[31]A 0 F   [СЧИТЫВАЕМ ВТОРОЙ ЭЛЕМЕНТ МАССИВА]
[32]T 2 F   [ЗАПИСЫВЕМ ВО ВТОРУЮ ЯЧЕЙКУ]

[33]A 6 F   [I]
[34]S 108 F [I - LENGTH]
[35]E 52 @  [>=0 МЫ СРАВНИВАЕМ С ПОСЛЕДНИМ ЭЛЕМЕНТОМ МАССИВА, ПЕРЕЙТИ К SWAP,
             ИНАЧЕ ПРОДОЛЖАТЬ]

[36]T 0 F   [ACC = 0]
[37]A 1 F   [СЧИТАЛИ ПЕРВУЮ ПЕРЕМЕННУЮ]
[38]S 2 F   [ВЫЧЛИ ВТОРУЮ ПЕРЕМЕННУЮ]
[39]E 21 @  [>=0 ЭТО НЕ ПОСЛЕДНИЙ ЭЛЕМЕНТ МАССИВА И ОН МЕНЬШЕ ПРЕДЫДУЩЕГО,
             МЕНЯТЬ НЕ НУЖНО]

[40]T 0 F   [ACC = 0]
[41]A 2 F   [СЧИТЫВАЕМ ВТОРУЮ ПЕРЕМЕННУЮ]
[42]T 1 F   [ЗАПИСЫВАЕМ В ЯЧЕЙКУ ДЛЯ ПЕРВОЙ ПЕРЕМЕННОЙ]
[43]A 5 F   [ЗАПИСЫВАЕМ В ЯЧЕЙКУ ДЛЯ АДРЕСА ПЕРВОГО]
[44]T 4 F   [ЗАПИСЫВАЕМ В ЯЧЕЙКУ ДЛЯ АДРЕСА ПЕРВОГО]
[45]A 2 F   [СЧИТАЛИ ВТОРУЮ ПЕРЕМЕННУЮ]
[46]S 2 F   [ОЧИСТИЛИ ЯЧЕЙКУ ДЛЯ ВТОРОЙ ПЕРЕМЕННОЙ]
[47]E 21 @  [ВОЗВРАТ В ЦИКЛ]

[48]T 0 F   [ACC = 0]
[49]A 1 F   [СЧИТАЛИ ПЕРВУЮ ПЕРЕМЕННУЮ]
[50]S 2 F   [ВЫЧЛИ ВТОРУЮ ПЕРЕМЕННУЮ]
[51]G 65 @  [<0 ЕСЛИ ВТОРАЯ ПЕРЕМЕННАЯ БОЛЬШЕ ПЕРВОЙ, ТО ПОСЛЕДНИЙ ЭЛЕМЕНТ
                НА ДАННЫЙ МОМЕНТ НЕ НУЖДАЕТСЯ В ПЕРЕСТАНОВКЕ, ПРОДЛЖАЕМ ЦИКЛ]

[52]T 0 F   [ACC = 0]
[53]A 4 F   [СЧИТЫВАЕМ АДРЕС ПЕРВОГО ЧИСЛА]
[54]L 0 D   [СДВИГ НА РАЗРЯД ВЛЕВО]
[55]A 104 F [ЗАГРУЖАЕМ КОМАНДУ T 0 S]
[56]T 58 @  [ЗАПИСЫВАЕМ ЕЁ В ЯЧЕЙКУ 58]
[57]A 2 F   [СЧИТЫВАЕМ ВТОРОЕ ЧИСЛО]
[58]T 0 F   [ЗАПИСЫВАЕМ НА ЕГО МЕТСТО ПЕРВОГО ЧИСЛА]
[59]A 5 F   [СЧИТЫВАЕМ АДРЕС ВТОРОГО ЧИСЛА]
[60]L 0 D   [СДВИГ НА РАЗРЯД ВЛЕВО]
[61]A 104 F [ЗАГРУЖАЕМ КОМАНДУ T 0 S]
[62]T 64 @  [ЗАПИСЫВАЕМ ЕЁ В ЯЧЕЙКУ 64]
[63]A 1 F   [СЧИТЫВАЕМ ПЕРВОЕ ЧИСЛО]
[64]T 0 F   [ЗАПИСЫВАЕМ НА ЕГО МЕСТО ВТОРОГО ЧИСЛА]

[65]T 0 F   [ACC = 0]
[66]A 108 F [LENGTH]
[67]S 107 F [LENTH --]
[68]T 108 F [ПЕРЕЗАПИСЬ LENGHT]

[69]A 106 F [0]
[70]S 108 F [-LENGTH]
[71]G 5 @   [<0 ПРОВЕРКА НА КОЛИЧЕСТВО ПРОЙДЕННЫЙ ЭЛЕМЕНТОВ]
[72]Z 0 F   [КОНЕЦ]

T 101 K
GK
[0:]T 0 F
[1:]A 2 @
[2:]G 130 F

[3:]T 0 F  [КОМАНДА ЗАПИСИ ЗНАЧЕНИЯ В ЯЧЕЙКУ ПО АДРЕСУ]
[4:]A 0 F  [КОМАНДА ЧТЕНИЯ ЗНАЧЕНИЯ ИЗ ЯЧЕЙКИ ПО АДРЕСУ]

[5:]P 0 F  [0]
[6:]P 0 D  [1]
[7:]P 2 F  [LENGTH = 4]
[8:]P 55 F [АДРЕС ПЕРВОГО ЭЛЕМЕНТА МАССИВА]

[9:] P 6 D [13]        
[10:]P 4 F [8]         
[11:]P 0 F [0]         
[12:]P 1 F [2]         

[13:]EZ PF
