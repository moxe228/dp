[Сортировка обменом массива чисел in-place. 110-113]
[31] T 114 S [КОНЕЦ ПРОГРАММЫ  + 1]
[32] T 0 S [ACC = 0]

[33] A 108 S [LENGTH]
[34] S 107 S [LENGTH --]
[35] T 108 S [ПЕРЕЗАПИСЬ LENGTH]

[36] T 0 S [ACC = 0]
[37] A 109 S [АДРЕС ПЕРВОГО ЭЛЕМЕНТА]
[38] U 4 S [ЗАПИСЫВАЕМ В 4 ЯЧЕЙКУ ДЛЯ ПЕРВОЙ ПЕРЕМЕННОЙ]
[39] T 5 S [ДЛЯ ВТОРОЙ ПЕРЕМЕННОЙ]

[40] A 106 S [0]
[41] T 6 S [I = 0]

[42] A 106 S [0]
[43] S 108 S [-LENGHT]
[44] E 103 S [>=0 ЗАКОНЧИЛИ ПРОХОД МАССИВА - ВЫХОД]

[45] T 0 S [ACC = 0]
[46] A 4 S [АДРЕС ПЕРВОГО ЭЛЕМЕНТА]
[47] L 0 L [СДВИГ НА РАЗРЯД ВЛЕВО]
[48] A 105 S [ЗАГРУЖАЕМ КОМАНДУ A 0 S]
[49] T 50 S [ЗАПИСЫВАЕМ ЕЁ В ЯЧЕЙКУ 50]
[50] A 0 S [СЧИТЫВАЕМ ПЕРВЫЙ ЭЛЕМЕНТ МАССИВА]
[51] T 1 S [ЗАПИСЫВАЕМ В ПЕРВУЮ ЯЧЕЙКУ]

[52] T 0 S [ACC = 0]
[53] A 6 S [I]
[54] A 107 S [I ++]
[55] T 6 S [ПЕРЕЗАПИСЬ I]

[56] A 5 S [СЧИТЫВАЕМ АДРЕС ПЕРВОГО СРАВНИВАЕМОГО ЧИСЛА]
[57] A 107 S [+1]
[58] U 5 S [ПЕРЕЗАПИСЫВАЕМ АДРЕС ВТОРОГО ЧИСЛА И ОСТАВЛЯЕМ В ACC]
[59] L 0 L [СДВИГ НА РАЗРЯД ВЛЕВО]
[60] A 105 S [ЗАГРУЖАЕМ КОМАНДУ A 0 S]
[61] T 62 S [ЗАПИСЫВАЕМ ЕЁ В ЯЧЕЙКУ 62]
[62] A 0 S [СЧИТЫВАЕМ ВТОРОЙ ЭЛЕМЕНТ МАССИВА]
[63] T 2 S [ЗАПИСЫВЕМ ВО ВТОРУЮ ЯЧЕЙКУ]

[64] A 6 S [I]
[65] S 108 S [I - LENGTH]
[66] E 79 S [>=0 МЫ СРАВНИВАЕМ С ПОСЛЕДНИМ ЭЛЕМЕНТОМ МАССИВА, ПЕРЕЙТИ К SWAP,
             ИНАЧЕ ПРОДОЛЖАТЬ]

[67] T 0 S [ACC = 0]
[68] A 1 S [СЧИТАЛИ ПЕРВУЮ ПЕРЕМЕННУЮ]
[69] S 2 S [ВЫЧЛИ ВТОРУЮ ПЕРЕМЕННУЮ]
[70] E 52 S [>=0 ЭТО НЕ ПОСЛЕДНИЙ ЭЛЕМЕНТ МАССИВА И ОН МЕНЬШЕ ПРЕДЫДУЩЕГО,
             МЕНЯТЬ НЕ НУЖНО]

[71] T 0 S [ACC = 0]
[72] A 2 S [СЧИТЫВАЕМ ВТОРУЮ ПЕРЕМЕННУЮ]
[73] T 1 S [ЗАПИСЫВАЕМ В ЯЧЕЙКУ ДЛЯ ПЕРВОЙ ПЕРЕМЕННОЙ]
[74] A 5 S [СЧИТЫВАЕМ АДРЕС ВТОРОГО ЭЛЕМЕНТА]
[75] T 4 S [ЗАПИСЫВАЕМ В ЯЧЕЙКУ ДЛЯ АДРЕСА ПЕРВОГО]
[76] A 2 S [СЧИТАЛИ ВТОРУЮ ПЕРЕМЕННУЮ]
[77] S 2 S [ОЧИСТИЛИ ЯЧЕЙКУ ДЛЯ ВТОРОЙ ПЕРЕМЕННОЙ]
[78] E 52 S [ВОЗВРАТ В ЦИКЛ]

[79] T 0 S [ACC = 0]
[80] A 1 S [СЧИТАЛИ ПЕРВУЮ ПЕРЕМЕННУЮ]
[81] S 2 S [ВЫЧЛИ ВТОРУЮ ПЕРЕМЕННУЮ]
[82] G 96 S [<0 ЕСЛИ ВТОРАЯ ПЕРЕМЕННАЯ БОЛЬШЕ ПЕРВОЙ, ТО ПОСЛЕДНИЙ ЭЛЕМЕНТ
                НА ДАННЫЙ МОМЕНТ НЕ НУЖДАЕТСЯ В ПЕРЕСТАНОВКЕ, ПРОДЛЖАЕМ ЦИКЛ]

[83] T 0 S [ACC = 0]
[84] A 4 S [СЧИТЫВАЕМ АДРЕС ПЕРВОГО ЧИСЛА]
[85] L 0 L [СДВИГ НА РАЗРЯД ВЛЕВО]
[86] A 104 S [ЗАГРУЖАЕМ КОМАНДУ T 0 S]
[87] T 89 S [ЗАПИСЫВАЕМ ЕЁ В ЯЧЕЙКУ 89]
[88] A 2 S [СЧИТЫВАЕМ ВТОРОЕ ЧИСЛО]
[89] T 0 S [ЗАПИСЫВАЕМ НА ЕГО МЕТСТО ПЕРВОГО ЧИСЛА]
[90] A 5 S [СЧИТЫВАЕМ АДРЕС ВТОРОГО ЧИСЛА]
[91] L 0 L [СДВИГ НА РАЗРЯД ВЛЕВО]
[92] A 104 S [ЗАГРУЖАЕМ КОМАНДУ T 0 S]
[93] T 95 S [ЗАПИСЫВАЕМ ЕЁ В ЯЧЕЙКУ 95]
[94] A 1 S [СЧИТЫВАЕМ ПЕРВОЕ ЧИСЛО]
[95] T 0 S [ЗАПИСЫВАЕМ НА ЕГО МЕСТО ВТОРОГО ЧИСЛА]

[96] T 0 S [ACC = 0]
[97] A 108 S [LENGTH]
[98] S 107 S [LENGTH --]
[99]T 108 S [ПЕРЕЗАПИСЬ LENGHT]

[100] A 106 S [0]
[101] S 108 S [- LENGTH]
[102] G 36 S [<0 ПРОВЕРКА НА КОЛИЧЕСТВО ПРОЙДЕННЫЙ ЭЛЕМЕНТОВ]
[103] Z 0 S [КОНЕЦ]

[104] T 0 S [КОМАНДА ЗАПИСИ ЗНАЧЕНИЯ В ЯЧЕЙКУ ПО АДРЕСУ]
[105] A 0 S [КОМАНДА ЧТЕНИЯ ЗНАЧЕНИЯ ИЗ ЯЧЕЙКИ ПО АДРЕСУ]

[106] P 0 S [0]
[107] P 0 L [1]
[108] P 2 S [LENGTH = 4]
[109] P 55 S [АДРЕС ПЕРВОГО ЭЛЕМЕНТА МАССИВА]

[110] P 22 L [45]
[111] P 9 S [18]
[112] P 45 S [90]
[113] P 37 S [74]
